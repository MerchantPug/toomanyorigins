import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'fabric-loom'
    id 'maven-publish'
    id 'idea'
    id "net.darkhax.curseforgegradle" version "1.0.7"
    id "com.modrinth.minotaur" version "2.+"
}

archivesBaseName = "${mod_name}"
version = "${rootProject.version}+${minecraft_version}-fabric"


loom {
    mixin.defaultRefmapName = "toomanyorigins.refmap.json"

    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}
repositories {
    maven {
        name = "Pug's Maven"
        url = 'https://maven.merchantpug.net/releases/'
    }
    maven {
        name = "Ladysnake Libs"
        url = 'https://maven.ladysnake.org/releases/'
    }
    maven {
        name = "JitPack"
        url = 'https://jitpack.io'
    }
    maven {
        name = "Cafeteria"
        url = 'https://maven.cafeteria.dev'
        content {
            includeGroup 'net.adriantodt.fabricmc'
        }
    }
    maven {
        name = "Jamie's White Shirt"
        url "https://maven.jamieswhiteshirt.com/libs-release"
        content {
            includeGroup "com.jamieswhiteshirt"
        }
    }
    maven {
        url "https://masa.dy.fi/maven"
    }
    maven {
        name = "Shedaniel"
        url "https://maven.shedaniel.me/"
    }
    maven {
        name = "TerraformersMC"
        url "https://maven.terraformersmc.com/"
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name = "QuiltMC"
        url = "https://maven.quiltmc.org/repository/release"
    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    mavenCentral()
    mavenLocal()
}

dependencies {
    //Minecraft
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
    }
    //Fabric
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    //ModMenu
    modImplementation "com.terraformersmc:modmenu:${modmenu_version}"
    //Origins
    modImplementation "com.github.apace100:origins-fabric:${origins_fabric_version}"
    //Apugli
    include(modImplementation("net.merchantpug:Apugli:${apugli_version}+${minecraft_version}-fabric"))
    // MixinExtras
    implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${mixin_extras_version}"))
    //JSR305
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    //Service
    compileOnly group:'com.google.auto.service', name:'auto-service', version:'1.0.1'
    annotationProcessor group:'com.google.auto.service', name:'auto-service', version:'1.0.1'
    //Relying on the Common Project for sure
    implementation project(path: ":common", configuration: "namedElements")
}

processResources {
    from project(":common").sourceSets.main.resources
    exclude "toomanyorigins.common.accesswidener"

    inputs.property "version", rootProject.version

    filesMatching("fabric.mod.json") {
        expand "version": rootProject.version
    }
}

processTestResources {
    outputs.upToDateWhen { false }
    from project(":common").sourceSets.test.resources
}

tasks.withType(JavaCompile) {
    source(project(":common").sourceSets.main.allSource)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            name = "merchantpug"
            url = "https://maven.merchantpug.net/releases"
            credentials {
                username System.getenv("MAVEN_USERNAME")
                password System.getenv("MAVEN_PASSWORD")
            }
            authentication {
                create("basic", BasicAuthentication)
            }
        }
        mavenLocal()
    }
}

tasks.register('curseforge', TaskPublishCurseForge) {
    disableVersionDetection()
    apiToken = System.getenv("CURSEFORGE_TOKEN")

    def mainFile = upload(cf_project_id, remapJar)
    mainFile.displayName = "Fabric v${rootProject.version}+${minecraft_version}"
    mainFile.releaseType = "release"
    mainFile.changelog = new File("$project.rootDir/CHANGELOG.md").getText('UTF-8')
    mainFile.changelogType = "markdown"
    fabric_mod_loaders.split(", ").each {
        mainFile.addModLoader(it)
    }
    fabric_game_versions.split(", ").each {
        mainFile.addGameVersion(it)
    }
    java_versions.split(", ").each {
        mainFile.addJavaVersion(it)
    }
    fabric_required_deps.split(", ").each {
        mainFile.addRequirement(it)
    }
    common_embedded_deps.split(", ").each {
        mainFile.addEmbedded(it)
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = mr_project_id
    versionNumber = "${version}"
    versionType = "release"
    versionName = "Fabric/Quilt v${rootProject.version}+${minecraft_version}"
    changelog = new File("$project.rootDir/CHANGELOG.md").getText('UTF-8')
    uploadFile = remapJar
    gameVersions = fabric_game_versions.split(", ").toList()
    loaders = fabric_mod_loaders.split(", ").collect { return it.toLowerCase(Locale.ROOT) }
    dependencies {
        fabric_required_deps.split(", ").each {
            required.project it
        }

        common_embedded_deps.split(", ").each {
            embedded.project it
        }
    }
}