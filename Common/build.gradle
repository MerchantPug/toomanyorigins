plugins {
    id 'java'
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'maven-publish'
    id "com.github.breadmoirai.github-release" version "2.4.1"
}

archivesBaseName = "${mod_name}"
version = "${rootProject.version}+${minecraft_version}-common"

repositories {
    maven {
        name = "Pug's Maven"
        url = 'https://maven.merchantpug.net/releases/'
    }
    maven {
        name = 'Sponge / Mixin'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
    maven {
        name = "JitPack"
        url = 'https://jitpack.io'
    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    mavenCentral()
    mavenLocal()
}

loom {
    accessWidenerPath = file("src/main/resources/toomanyorigins.common.accesswidener")
    shareCaches()
    mixin {
        useLegacyMixinAp = false
    }
}

dependencies {
    // Minecraft
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
    }
    // Origins
    modCompileOnly("com.github.apace100:calio:${calio_fabric_version}") { transitive false }
    modCompileOnly("com.github.apace100:apoli:${apoli_fabric_version}") { transitive false }
    modCompileOnly("com.github.apace100:origins-fabric:${origins_fabric_version}") { transitive false }
    // Apugli
    modCompileOnly("net.merchantpug:Apugli:${apugli_version}+${minecraft_version}-fabric") { transitive false }
    // MixinExtras
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:${mixin_extras_version}"))
    //Mixin
    compileOnly group:'org.spongepowered', name:'mixin', version:'0.8.5'
    compileOnly group: 'org.ow2.asm', name: 'asm-tree', version: '9.4'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact jar
            artifact sourcesJar
        }
    }

    repositories {
        maven {
            name = "merchantpug"
            url = "https://maven.merchantpug.net/releases"
            credentials(PasswordCredentials)
            authentication {
                create("basic", BasicAuthentication)
            }
        }
        mavenLocal()
    }
}

githubRelease {
    if (System.getenv("GITHUB_TOKEN") == null) return
    token = System.getenv("GITHUB_TOKEN")
    owner = gh_owner
    repo = gh_repo
    tagName = "${rootProject.version}+${minecraft_version}"
    targetCommitish = gh_commitish
    releaseName = "$project.mod_name v${rootProject.version}+${minecraft_version}"
    body {
        new File("$project.rootDir/CHANGELOG.md").getText('UTF-8')
    }
}

tasks.remapJar {
    enabled = false
}

tasks.remapSourcesJar {
    enabled = false
}